channel a, b, c, d, e, f 

P = 
    let
        P1 = a -> P2
        P2 = c -> P2
             []
             e -> P1
    within 
        P1

Q = 
    let
        Q1 = b -> Q2 
             |~|
             c -> Q2
        Q2 = d -> Q1
    within 
        Q1

R =
    let
        R1 = d -> R2
             []
             e -> R2
        R2 = f -> R1
    within 
        R1

System = 
    ( P [| {c} |] Q ) [| {d, e} |] R

-- (a)

PQ = 
    let
        P1Q1 = 
            (a -> P2Q1)
            []
            ((a -> P2Q1)
            |~|
            (b -> P1Q2))
        P1Q2 = 
            (a -> P2Q2)
            []
            (d -> P1Q1)
        P2Q1 = 
            ((b -> P2Q2)
            |~|
            (c -> P2Q2))
            []
            (e -> P1Q1)
        P2Q2 = 
            (e -> P1Q2)
            []
            (d -> P2Q1)
    within
        P1Q1

assert P [| {c} |] Q [T= PQ 
assert PQ [T= P [| {c} |] Q
assert P [| {c} |] Q [FD= PQ
assert PQ [FD= P [| {c} |] Q

-- (b)

PQR = 
    let
        P1Q1R1 = 
            (a -> P2Q1R1)
            []
            ((a -> P2Q1R1) 
            |~|
            (b -> P1Q2R1))
        P1Q1R2 = 
            (a -> P2Q1R2)
            []
            ((a -> P2Q1R2) 
            |~|
            (b -> P1Q2R2))
            []
            (f -> P1Q1R1)
        P1Q2R1 = 
            (a -> P2Q2R1)
            []
            (d -> P1Q1R2)
        P1Q2R2 = 
            (a -> P2Q2R2)
            []
            (f -> P1Q2R1)
        P2Q1R1 = 
            ((b -> P2Q2R1)
            |~|
            (c -> P2Q2R1))
            []
            (e -> P1Q1R2)
        P2Q1R2 = 
            ((b -> P2Q2R2)
            |~|
            (c -> P2Q2R2))
            []
            (f -> P2Q1R1)
        P2Q2R1 = 
            (e -> P1Q2R2)
            []
            (d -> P2Q1R2)
        P2Q2R2 = 
            (f -> P2Q2R1)
    within
        P1Q1R1

assert System [T= PQR
assert PQR [T= System
assert System [FD= PQR
assert PQR [FD= System

-- (c)

SystemH = System \ {c, d, e}

-- (i)

SpecI =
    let
        AB = 
            (a -> F)
            []
            (b -> F)
        F = 
            (f -> AB) 
    within
        AB

assert SystemH [T= SpecI

-- (ii)

SpecII = 
    let 
        AB =
            (a -> (f -> AB) |~| AB) 
            |~|
            (b -> (f -> AB) |~| AB)
    within
        AB

assert SpecII [FD= SystemH

-- (iii)

Hidden = 
     let
        H1 = (a -> H9) [] (H2 |~| H13)
        H2 = (a -> H3)
        H3 = H4 |~| H7
        H4 = (a -> H5) [] (f -> H2)
        H5 = (f -> H3) [] (STOP |~| H6)
        H6 = (f -> H7)
        H7 = (STOP |~| H8)
        H8 = (f -> H9) [] (STOP |~| H5)
        H9 = H3 |~| H10 |~| H15
        H10 = (a -> H8) [] (f -> H1) |~| H4 |~| H11
        H11 = (a -> H12) [] (b -> H14) [] (f -> H13)
        H12 = (b -> H6) [] (f -> H15)
        H13 = (a -> H15) [] (b -> H16)
        H14 = (a -> H6) [] (f -> H16)
        H15 = (b -> H7) [] (STOP |~| H11)
        H16 = (a -> H7) [] (STOP |~| H10)
    within
        H1

assert SystemH [T= Hidden
assert Hidden [T= SystemH 
assert SystemH [FD= Hidden
assert Hidden [FD= SystemH